textEntry = require("app/common/textEntry.tui")
button = require("app/common/button.tui")
defaultSiteScene = require("app/katipoBrowser/scripts/defaultSiteScene.tui")

print("katipoBrowser is running")

bookmarksPath = file.getSavePath("bookmarks.tuib") #todo use database
bookmarks = file.loadBinary(bookmarksPath)
if(!bookmarks)
{
    bookmarks = {}
}

print("loaded bookmarks:", bookmarks)

urlTextEntryContainer = scene.getView("urlTextEntry")
urlTextEntry = textEntry.create(urlTextEntryContainer)

windowSafeBackground = scene.getView("windowSafeBackground")
windowSafeBackground.size = function(parentSize)
{
    windowSafeArea = eventManager.getWindowSafeArea()
    #print("parentSize:", parentSize, " windowSafeArea:", windowSafeArea)
    windowSafeBackground.pos = vec3(windowSafeArea.x, -windowSafeArea.y, 0)
    return vec2(windowSafeArea.z, windowSafeArea.w)
}

siteContentBackground = scene.getView("siteContent")
siteContentBackground.update = function(dt)
{
    if(siteContentBackground.goalColor and siteContentBackground.goalColor != siteContentBackground.color)
    {
        siteContentBackground.color = math.mix(siteContentBackground.color, siteContentBackground.goalColor, dt * 4.0)
    }
}

defaultSiteContent = siteContentBackground.addView(defaultSiteScene.mainView)
defaultSiteContent.hidden = true

pageTitle = defaultSiteContent.getView("pageTitle")
pageBody = defaultSiteContent.getView("pageBody")

bookmarkLayout = scene.bookmark
bookmarksBackground = scene.getView("bookmarks")

untrustedSiteCodePermissionFunction = function(functionName, args, hasPermissionCallback)
{
    print("Warning: host is attempting to use protected tui function:\"", functionName, "\" with args:", args, "\nTODO: Katipo needs to check client settings or ask if this is OK, but for now it is not OK")
    hasPermissionCallback(false)
}

getCallback = function(result, hostID, url)
{
    print("got result with status:", result.status, " message:", result.message, " hostID:", hostID)
    if(!result or result.status != "ok")
    {
        siteContentBackground.goalColor = vec4(0.2,0.0,0.0,0.6)
        defaultSiteContent.hidden = false

        pageTitle.text = result.status
        pageBody.text = result.message
    }
    else if(result.pageTitle)
    {
        siteContentBackground.goalColor = vec4(0.0,0.0,0.2,0.4)
        defaultSiteContent.hidden = false

        pageTitle.text = result.pageTitle

        if(result.pageBody)
        {
            pageBody.text = result.pageBody
        }
        else
        {
            pageBody.text = ""
        }
    }
    else if(result.siteData)
    {
        siteContentBackground.goalColor = vec4(0.0,0.2,0.2,0.4)
        defaultSiteContent.hidden = true

        siteSavePath = file.getSavePath("browserSites/" + string.subString(hostID, 0, 6) + "/")
        scriptsDirPath = siteSavePath + "scripts/"
        fontsDirPath = siteSavePath + "fonts/"
        imagesDirPath = siteSavePath + "img/"
        filesDirPath = siteSavePath + "files/"
        foundSiteCode = false

        if(result.siteData.scripts and table.count(result.siteData.scripts) > 0)
        {
            file.mkdir(scriptsDirPath)
            for(scriptInfo in result.siteData.scripts)
            {
                scriptPath = scriptsDirPath + scriptInfo.fileName
                if(file.isSubPath(scriptPath, siteSavePath))
                {
                    file.saveData(scriptPath, scriptInfo.data)
                }

                if(scriptInfo.fileName == "code.tui") #we actually need scene.tui too
                {
                    foundSiteCode = true
                }
            }
        }
        if(foundSiteCode)
        {
            print("saved ", table.count(result.siteData.scripts), " scripts")
            if(result.siteData.images and table.count(result.siteData.images) > 0)
            {
                file.mkdir(imagesDirPath)
                for(imageInfo in result.siteData.images)
                {
                    imagePath = imagesDirPath + imageInfo.fileName
                    if(file.isSubPath(imagePath, siteSavePath))
                    {
                        file.saveData(imagePath, imageInfo.data)
                    }
                }
                print("saved ", table.count(result.siteData.images), " images")
            }

            if(result.siteData.fonts and table.count(result.siteData.fonts) > 0)
            {
                file.mkdir(fontsDirPath)
                for(fontInfo in result.siteData.fonts)
                {
                    fontPath = fontsDirPath + fontInfo.fileName
                    if(file.isSubPath(fontPath, siteSavePath))
                    {
                        file.saveData(fontPath, fontInfo.data)
                    }
                }
                print("saved ", table.count(result.siteData.fonts), " fonts")
            }

            if(result.siteData.files and table.count(result.siteData.files) > 0)
            {
                print("files")
                file.mkdir(filesDirPath)
                for(fileInfo in result.siteData.files)
                {
                    filePath = filesDirPath + fileInfo.fileName
                    if(file.isSubPath(filePath, siteSavePath))
                    {
                        #print("filepath:", filePath)
                        if(type(fileInfo.data) == "string")
                        {
                            print("saved raw data:", filePath)
                            file.saveData(filePath, fileInfo.data) #write the string data (can be binary) to disk directly
                        }
                        else
                        {
                            file.saveBinary(filePath, fileInfo.data) #save the tui object as a binary export
                            print("saved tui object:", filePath)
                        }
                    }
                }
            }

            print("loading site code")
            katipo.loadSite(hostID, siteSavePath, untrustedSiteCodePermissionFunction)

            #update bookmarks
            for(i, existing in bookmarks)
            {
                if(existing.hostID == hostID)
                {
                    table.remove(bookmarks, i)
                    break
                }
            }

            bookmarkInfo = {
                url = url
                hostID = hostID
                siteInfo = result.siteData.siteInfo
                #siteImage = result.siteData.siteImage
            }
            table.insert(bookmarks, 0, bookmarkInfo)

            file.saveBinary(bookmarksPath, bookmarks) #todo use database
        }
        else
        {
            print("warning: no site code found")
            defaultSiteContent.hidden = false
            pageTitle.text = "no site code found"
            pageBody.text = ""
        }
    }
    else
    {
        print("warning: unhandled result:", result)
        defaultSiteContent.hidden = false
        pageTitle.text = "unhandled result"
        pageBody.text = ""
    }
}

textEntry.setOnChangeCallback(urlTextEntry, function(textValue, confirmChanges){
    if(confirmChanges and string.length(textValue) > 0)
    {
        siteContentBackground.goalColor = vec4(0.0,0.015,0.01,0.6)
        katipo.get(textValue, getCallback)
        bookmarksBackground.hidden = true
    }
})


loadBookmarks = function()
{

    for(i,bookmarkInfo in bookmarks)
    {
        #todo some kind of scrolling saved spacial system like a Desktop, for now they will pile up on top of each other
        bookmarkView = bookmarksBackground.addView(bookmarkLayout)
        title = bookmarkInfo.siteInfo.title
        bookmarkTitleView = bookmarkView.getView("bookmarkTitle")
        bookmarkTitleView.text = title

        if(bookmarkInfo.siteImage)
        {
            #todo
        }
        else
        {
            bookmarkMissingImage = bookmarkView.getView("bookmarkMissingImage")
            #bookmarkMissingImage.color = vec4() #todo
            bookmarkMissingImageCharacter = bookmarkView.getView("bookmarkMissingImageCharacter")
            bookmarkMissingImageCharacter.text = string.subString(title, 0, 1)
        }

        bookmarkClick = function()
        {
            siteContentBackground.goalColor = vec4(0.0, 0.015, 0.01, 0.6)
            katipo.get(bookmarkInfo.url, getCallback)
            bookmarksBackground.hidden = true
        }
        button.create(bookmarkView, -0.1, bookmarkClick)
    }
}

loadBookmarks()